/*
 * Copyright (c) 2015. Ronald D. Kurr kurr@jvmguy.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


ext {
    generatedDocumentation = file('build/generated-snippets')
}

dependencies {
    testCompile 'org.springframework.restdocs:spring-restdocs:0.1.0.BUILD-SNAPSHOT'
}

test {
    systemProperty 'org.springframework.restdocs.outputDir', generatedDocumentation
    outputs.dir generatedDocumentation
}

asciidoctor {
    attributes 'generated': generatedDocumentation
    inputs.dir generatedDocumentation
}

task copyApiDocs( type: Copy, dependsOn: asciidoctor ) {
    from "${buildDir}/asciidoc/html5"
    into "${buildDir}/api-docs/static/api-docs/"
}

task updateJarWithApiDocumentation( dependsOn: copyApiDocs ) {
    group = 'Documentation'
    description = 'Add the API documentation to the JAR file'
    doLast {
        ant.jar( update: 'true',
                 duplicate: 'fail',
                 compress: 'false',
                 destfile: "${libsDir}/${archivesBaseName}-${version}.jar") {
            fileset( dir: "${buildDir}/api-docs" )
        }
    }
}
